apply plugin: 'com.google.protobuf'
apply plugin: 'org.jetbrains.kotlin.jvm'

dependencies {
    api group: 'io.grpc', name: 'grpc-netty', version: "${grpc_version}"
    api group: 'io.grpc', name: 'grpc-protobuf', version: "${grpc_version}"
    api group: 'io.grpc', name: 'grpc-stub', version: "${grpc_version}"
    api group: 'io.grpc', name: 'grpc-services', version: "${grpc_version}"
    api group: 'io.grpc', name: 'grpc-kotlin-stub', version: "${grpc_kotlin_version}"

    api group: 'com.google.protobuf', name: 'protobuf-kotlin', version: "${protobuf_version}"
    api group: 'com.google.protobuf', name: 'protobuf-java-util', version: "${protobuf_version}"

    implementation 'javax.annotation:javax.annotation-api:1.3.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

sourceSets {
    main.java.srcDirs = [
        "${buildDir}/generated/source/proto/main/java",
        "${buildDir}/generated/source/proto/main/grpc"
    ]
    main.kotlin.srcDirs = [
        "${buildDir}/generated/source/proto/main/grpckt",
        "${buildDir}/generated/source/proto/main/kotlin"
    ]
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobuf_version}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }

        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:${grpc_kotlin_version}:jdk7@jar"
        }

        kotlin {}

        doc {
            artifact = "io.github.pseudomuto:protoc-gen-doc:1.5.1"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
            grpckt {}
            kotlin {}
            doc {
                option "${protoDocType},${protoDocFile}"
            }
        }
    }
}